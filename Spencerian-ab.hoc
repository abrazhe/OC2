// Morphology based on Spencer et al 2012 Front. comp. neurosci
// Currents based on Rothman and Manis 2003, Bal and Oertel 2000, 2001


// celsius = 33 // Does it matter where to set celsius, inside soma or not?


ndend = 4

create soma, ax_stub, ax_init, dendrites[ndend]

// --- biophysics --- 
soma {
    diam = 25 // Spencer. RM-03 have 21, which is close. also, OC could be larger than BC
    L = 25
    nseg = 1
    
    insert kltab ek_kltab = -70 // following RM-03
    insert hcno eh_hcno = -38   // taken from BO-00
    insert khtab ek_khtab = -70 // after RM-03
    
    gbar_kltab = 0.0407 // mho/cm2, following Spencer12
    gbar_khtab = 0.006  // mho/cm2, ---------//--------
    gbar_hcno = 0.0076     // mho/cm2, ---------//--------
    
    
}


// TODO: :geometry: Ususally, axons are much thinner than dendrites, not the case with
// Spencer et al 2012 model

ax_stub {
    diam = 3
    L = 10
    Ra = 100
}

ax_init {
    diam = 3
    L = 2
    Ra = 100
    insert naab ena = 55 gbar_naab = 4.244
}

for i = 0, ndend-1 dendrites[i] {
    diam = 3
    L = 250
    nseg = 20
    insert kltab ek_kltab = -70 
    insert hcno eh_hcno = -38
    
    gbar_kltab = soma.gbar_kltab*0.1
    gbar_hcno = soma.gbar_hcno*0.1
    
    
}

forall {
    Ra = 150 // RM-03, BO-00
    cm = 1
    insert leak  // leak is from RM-03   
    gbar_leak = 0.002 // Spencer12
    
}

access soma


// --- done with biophysics 


// --- Stimulation ---

objectvar soma_stim
soma soma_stim = new IClamp(0.5)

soma_stim.del = 100
soma_stim.dur = 100
soma_stim.amp = 0.1


// --- done stimulation ---

// --- Connections --- 

connect ax_init(0), ax_stub(1)
connect ax_stub(0), soma(0)

for i=0, ndend-1 {
    connect dendrites[i](0), soma(1) 
}
// --- done connections --- 




celsius = 37

// --- Numerics --- 
//dt = 0.01
tstop = 300
v_init = -65

// --- Graphs ---

objref vg

vg = new Graph()

addplot(vg, 0)
vg.size(0, tstop, -80, 20)
vg.addvar("soma.v(0.5)", 1, 1, 0.6, 0.9, 2)
vg.exec_menu("Keep Lines")

// --- Running this sh*t --- 
//initialize()
run()

//celsius = 37

soma_stim.amp = -0.1
run()


soma_stim.amp = 5.0
run()