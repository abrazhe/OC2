// Morphology based on Spencer et al 2012 Front. comp. neurosci
// Currents based on Rothman and Manis 2003, Bal and Oertel 2000, 2001


// celsius = 33 // Does it matter where to set celsius, inside soma or not?


ndend = 4

create soma, ax_stub, ax_init, dendrites[ndend]

// --- biophysics --- 
soma {
    diam = 25 // Spencer. RM-03 have 21, which is close. also, OC could be larger than BC
    L = 25
    nseg = 1
    
    insert kltab ek_kltab = -70 // following RM-03
    insert khtab ek_khtab = -70 // after RM-03
    insert hcno eh_hcno = -38   // taken from BO-00
    insert ihsab eh_ihsab = -38   // as in Spencer
    
    gbar_kltab = 0.0407 // mho/cm2, following Spencer12
    gbar_khtab = 0.0061  // mho/cm2, ---------//--------
    //gbar_hcno = 0.0076     // mho/cm2, ---------//--------
    gbar_hcno = 0 // swit it off
    gbar_ihsab = 0.0076 // following Spencer12
    
    
}


// TODO: :geometry: Ususally, axons are much thinner than dendrites, not the case with
// Spencer et al 2012 model

ax_stub {
    diam = 3
    L = 10
}

ax_init {
    diam = 3
    L = 20
    insert nasab ena = 55 gbar_nasab = 4.244
    //insert naab ena = 55 gbar_naab = 4.244
}

for i = 0, ndend-1 dendrites[i] {
    diam = 3
    L = 250
    nseg = 20
    insert kltab ek_kltab = -70 
    insert hcno eh_hcno = -38
    insert ihsab eh_ihsab = -38   // as in Spencer
    
    gbar_kltab = soma.gbar_kltab*0.1
    gbar_hcno = soma.gbar_hcno*0.1
    
    
}

forall {
    //Ra = 150 // RM-03, BO-00
    Ra = 100  //Spencer
    cm = 0.9 //Spencer
    insert leak  // leak is from RM-03   
    gbar_leak = 0.002 // S/cm2 Spencer12
    
}

access soma


// --- done with biophysics 


// --- Stimulation ---

objectvar soma_stim[2]

soma soma_stim[0] = new IClamp(0.5)
soma soma_stim[1] = new IClamp(0.5)


proc two_pulse_stim() {
    
    for i =0,1 {
        soma_stim[i].dur = 10
        soma_stim[i].amp = $1 // nA
        soma_stim[i].del = 50
    }
    soma_stim[1].del = 50 + 10
    soma_stim[1].amp = 2*$1 //nA
    run()
}

proc hyperpol_stim() {
    soma_stim[0].amp = $1
    soma_stim[0].del = 250
    soma_stim[0].dur = 50
    soma_stim[1].amp = 0
    run()
} 

// --- done stimulation ---

// --- Connections --- 

connect ax_init(0), ax_stub(1)
connect ax_stub(0), soma(0)

for i=0, ndend-1 {
    connect dendrites[i](0), soma(1) 
}
// --- done connections --- 

celsius = 33

// --- Numerics --- 
//dt = 0.01
tstop = 100
v_init = -62

// --- Graphs ---

objref vg


vg = new Graph()

addplot(vg, 0)
vg.size(0, tstop, -80, -20)
vg.addvar("soma.v(0.5)", 1, 1, 0.6, 0.9, 2)
vg.addvar("ax_init.v(0.5)", 2, 1, 0.6, 0.9, 2)
vg.exec_menu("Keep Lines")

// --- Running this sh*t --- 
//initialize()

two_pulse_stim(3)
//two_pulse_stim(5)

//celsius = 37

//hyperpol_stim(-10)